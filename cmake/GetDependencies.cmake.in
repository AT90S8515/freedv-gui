# As this script is run in a new cmake instance, it does not have access to
# the existing cache variables. Pass them in via the configure_file command.
set(CMAKE_BINARY_DIR @CMAKE_BINARY_DIR@)
set(CMAKE_SOURCE_DIR @CMAKE_SOURCE_DIR@)
set(USE_INTERNAL_CODEC2 @USE_INTERNAL_CODEC2@)
set(UNIX @UNIX@)
set(WIN32 @WIN32@)
set(CMAKE_CROSSCOMPILING @CMAKE_CROSSCOMPILING@)
set(CMAKE_FIND_LIBRARY_SUFFIXES @CMAKE_FIND_LIBRARY_SUFFIXES@)
set(CMAKE_FIND_LIBRARY_PREFIXES @CMAKE_FIND_LIBRARY_PREFIXES@)
set(CMAKE_SYSTEM_LIBRARY_PATH @CMAKE_SYSTEM_LIBRARY_PATH@)
set(CMAKE_FIND_ROOT_PATH @CMAKE_FIND_ROOT_PATH@)
if(USE_INTERNAL_CODEC2)
    set(CODEC2_BUILD_DIR ${CMAKE_BINARY_DIR}/codec2_build)
    set(LPCNET_BUILD_DIR ${CMAKE_BINARY_DIR}/LPCNet_build)
else()
    set(CODEC2_BUILD_DIR @CODEC2_BUILD_DIR@)
    set(LPCNET_BUILD_DIR @LPCNET_BUILD_DIR@)
endif()


set(FREEDV_EXE ${CMAKE_BINARY_DIR}/src/freedv.exe)

include(GetPrerequisites)
get_prerequisites("${FREEDV_EXE}" _deps 1 0 "" "")
if(CODEC2_BUILD_DIR)
    list(FIND _deps libcodec2.dll _loc)
    list(REMOVE_AT _deps ${_loc})
    find_library(CODEC2 codec2 NO_DEFAULT_PATH PATHS ${CODEC2_BUILD_DIR}/src)
    file(INSTALL DESTINATION "${CMAKE_INSTALL_PREFIX}/bin"
        TYPE EXECUTABLE FILES ${CODEC2})
endif()
if(LPCNET_BUILD_DIR)
    list(FIND _deps liblpcnetfreedv.dll _loc)
    list(REMOVE_AT _deps ${_loc})
    find_library(LPCNET lpcnetfreedv NO_DEFAULT_PATH PATHS ${LPCNET_BUILD_DIR}/src)
    file(INSTALL DESTINATION "${CMAKE_INSTALL_PREFIX}/bin"
        TYPE EXECUTABLE FILES ${LPCNET})
endif()

foreach(_runtime ${_deps})
    message("Looking for ${_runtime}")
    find_library(RUNTIME_${_runtime} ${_runtime})
    if(RUNTIME_${_runtime})
        message("Found ${RUNTIME_${_runtime}}")
        message("Looking for dependencies of ${_runtime}")
        get_prerequisites("${RUNTIME_${_runtime}}" _deps2 1 0 "" "")
        foreach(_runtime2 ${_deps2})
            find_library(RUNTIME_${_runtime2} ${_runtime2})
            if(RUNTIME_${_runtime2})
                message("${RUNTIME_${_runtime2}}")
                file(INSTALL DESTINATION "${CMAKE_INSTALL_PREFIX}/bin"
                TYPE EXECUTABLE FILES "${RUNTIME_${_runtime2}}")
            endif()
        endforeach()
    endif()
    if(RUNTIME_${_runtime})
        file(INSTALL DESTINATION "${CMAKE_INSTALL_PREFIX}/bin"
        TYPE EXECUTABLE FILES "${RUNTIME_${_runtime}}")
    endif()
endforeach()
